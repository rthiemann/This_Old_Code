100 TEXT : HOME : CLEAR
110 REM
120 REM

=======
SCANNER
=======

130 REM
    PAUL RAYMER
    PAUL'S ELECTRIC COMPUTER
    BOX 42831
    LAS VEGAS NEVADA 89116

140 REM VI / XXVI / MCMLXXXII
150 REM
160 REM
170 REM
180 REM
190 N = - 16336
200 FOR X = 1 TO 39:L$ = L$ + "=": NEXT X
210 VTAB 5: PRINT "IS THIS THE FIRST TIME YOU'VE PLAYED    'SCANNER'? Y/N ";: GET A$: HOME : IF A$ = "Y" THEN GOSUB 820
220 SPEED= 39: VTAB 2: HTAB 15: PRINT "THE SCANNER": FOR Z = 1 TO 1000: NEXT Z: SPEED= 255
230 FOR X = 1 TO 39
240 R = INT (26 * RND (1)) + 1
250 POKE 1151 + X,R + 128
260 P = PEEK (N)
270 NEXT X
280 VTAB 23: HTAB 2 : CALL - 868 : PRINT "SELECT LETTER => ";: GET A$: VTAB 23: HTAB 2: CALL - 868:J = 1
290 IF A$ = CHR$ (46) THEN A$ = CHR$ (110)
300 B = ASC (A$) + 64
310 REM

=========
SCAN LINE
=========

320 FOR X = 1 TO 40
330 S = PEEK (1151 + X): IF S = B THEN GOTO 360
340 NEXT X
350 GOTO 750
360 REM

=======
DROPPER
=======

370 VTAB 16: HTAB 1: PRINT L$
380 POKE 1151 + X,160
390 FOR Y = 3 TO 15
400 VTAB Y - 1: HTAB X: PRINT CHR$ (32)
410 IF S = 174 THEN S = 110
420 VTAB Y: HTAB X: PRINT CHR$ (S - 64)
430 FOR Z = 1 TO 20: NEXT Z
440 P = PEEK (N)
450 NEXT Y
460 REM

=======
CATCHER
=======

470 FOR X = 1 TO 40
480 F = PEEK (1831 + X)
490 IF F < > 160 THEN I = F
500 NEXT X
510 REM

========
CLEAN-UP
========

520 FOR X = 40 TO 1 STEP - 1
530 POKE 1831 + X,93: POKE 1831 + X + 1,160
540 FOR Z = 1 TO 30: NEXT Z
550 P = PEEK (N)
560 NEXT X
570 POKE 1832, 160
580 FOR X = 16 TO 19: VTAB X: HTAB 1: PRINT "J": VTAB X - 1: HTAB 1: PRINT " ": FOR Z = 1 TO 20: NEXT Z: NEXT X
590 POKE 1360, 160
600 POKE 1488 + T,I:T = T + 1: IF T > 39 THEN GOTO 760
610 REM

========
CLOSE-UP
========

620 FOR E = J TO 39
630 C = PEEK (1151 + E)
640 IF C = 160 THEN 670
650 NEXT E
660 GOTO 280
670 H = PEEK (1152 + E)
680 POKE 1151 + E,H
690 POKE 1152 + E,160
700 POKE 1191,174
710 J = E
720 P = PEEK (N)
730 GOTO 620
740 END
750 REM

=======
COUNTER
=======

760 FOR X = 1 TO 40
770 U = PEEK (1487 + X)
780 IF U = 174 THEN O = O + 1
790 NEXT X
800 VTAB 23: HTAB 1: CALL - 868: PRINT "TOTAL WORDS = ";O;
810 END
820 REM

===========
HOW TO PLAY
===========
830 SPEED= 100: VTAB 5: PRINT "SPELL WORDS FROM THE ASSORTED LETTERS   DISPLAYED...ONE LETTER AT A TIME --     WHEN ASKED. WHEN YOU FINISH A WORD...   SELECT A <PERIOD>.": PRINT : PRINT "TO QUIT GAME...PRESS SPACE BAR."
840 PRINT : PRINT "IF YOU SELECT A LETTER WHICH IS NOT IN  THE DISPLAY LINE...THE GAME WILL END    AUTOMATICALLY."
850 SPEED= 255
860 VTAB 19: HTAB 1: PRINT "PRESS ANY KEY TO BEGIN GAME ... "
870 VTAB 22: HTAB 1: PRINT "FOR SILENT VERSION PRESS <SPACE BAR>       ======"
880 VTAB 19: HTAB 32: GET A$
890 IF A$ = CHR$ (32) THEN N = 0
900 HOME
910 RETURN
